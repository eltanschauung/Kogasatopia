#pragma semicolon 1
#pragma newdecls required

#include <sourcemod>
#include <files>

#undef REQUIRE_PLUGIN
#include <dgm_api>
#define REQUIRE_PLUGIN

ConVar g_cvManualBotQuota;
ConVar g_cvGameBotQuota;

public Plugin myinfo = {
    name = "TF2 Bot Toggle Command",
    author = "Hombre",
    description = "Toggle TF2 bots on or off conveniently",
    version = "1.3",
    url = "https://kogasa.tf"
};

public APLRes AskPluginLoad2(Handle self, bool late, char[] error, int errMax)
{
    MarkNativeAsOptional("DGM_RealPlayerCount");
    return APLRes_Success;
}

public void OnPluginStart()
{
    g_cvManualBotQuota = CreateConVar("sm_tf_bot_quota", "8",
        "Manually control bot quotas with Sourcemod, e.g. leaving an entry per-map or per-gamemode file",
        _, true, 0.0, true, 32.0);

    HookConVarChange(g_cvManualBotQuota, OnBotQuotaChanged);

    RegConsoleCmd("sm_bots", Command_BotToggle, "Allows players to toggle bots on and off with a convenient command");
    g_cvGameBotQuota = FindConVar("tf_bot_quota");
    if (g_cvGameBotQuota == null)
    {
        SetFailState("Required convar tf_bot_quota was not found.");
    }

    // Build absolute paths relative to tf/cfg/
    char botsCfg[PLATFORM_MAX_PATH];
    char noBotsCfg[PLATFORM_MAX_PATH];
    BuildPath(Path_SM, botsCfg, sizeof(botsCfg), "../../cfg/bots.cfg");
    BuildPath(Path_SM, noBotsCfg, sizeof(noBotsCfg), "../../cfg/nobots.cfg");

    // Ensure both files exist
    BotConfigFiles(botsCfg, noBotsCfg);

    HookEvent("player_connect", Event_PlayerConnect, EventHookMode_Pre);
    HookEvent("player_connect_client", Event_PlayerConnect, EventHookMode_Pre);
    HookEvent("player_changename", Event_PlayerChangeName, EventHookMode_Pre);
}

public void OnBotQuotaChanged(ConVar convar, const char[] oldValue, const char[] newValue)
{
    int quota = g_cvManualBotQuota.IntValue;
    ServerCommand("tf_bot_quota %i", quota);
    LogMessage("[Bots] sm_tf_bot_quota changed from %s to %s (executed tf_bot_quota %i)", oldValue, newValue, quota);
}

public Action Command_BotToggle(int client, int args)
{
    if (client == 0)
        return Plugin_Continue;

    int quota = g_cvGameBotQuota.IntValue;

    if (quota > 0)
    {
        ServerCommand("exec nobots.cfg");
        PrintToChat(client, "[Bots] Bots disabled");
    }
    else
    {
        if (GetFeatureStatus(FeatureType_Native, "DGM_RealPlayerCount") == FeatureStatus_Available
            && DGM_RealPlayerCount() >= 8)
        {
            PrintToChat(client, "[Bots] Cannot enable bots with 8 or more real players.");
            return Plugin_Handled;
        }

        int smQuota = g_cvManualBotQuota.IntValue;
        if (smQuota != 8)
        {
            ServerCommand("tf_bot_quota %i", smQuota);
            PrintToChat(client, "[Bots] Bots enabled, quota %i", smQuota);
            return Plugin_Handled;
        }
        ServerCommand("exec bots.cfg");
        PrintToChat(client, "[Bots] Bots enabled");
    }
    return Plugin_Handled;
}

public Action Event_PlayerConnect(Event event, const char[] name, bool dontBroadcast)
{
    bool isBot = GetEventBool(event, "bot");
    int client = GetClientOfUserId(GetEventInt(event, "userid"));
    if (!isBot && client > 0 && IsClientInGame(client))
    {
        isBot = IsFakeClient(client);
    }

    if (isBot)
    {
        SetEventBroadcast(event, true);
        return Plugin_Handled;
    }

    return Plugin_Continue;
}

public Action Event_PlayerChangeName(Event event, const char[] name, bool dontBroadcast)
{
    int client = GetClientOfUserId(GetEventInt(event, "userid"));
    if (client > 0 && IsClientInGame(client) && IsFakeClient(client))
    {
        SetEventBroadcast(event, true);
        return Plugin_Handled;
    }

    return Plugin_Continue;
}

void BotConfigFiles(const char[] botsCfg, const char[] noBotsCfg)
{
    // bots.cfg defaults
    if (!FileExists(botsCfg))
    {
        File botsFile = OpenFile(botsCfg, "w");
        if (botsFile != null)
        {
            botsFile.WriteLine("// Auto-generated bots.cfg");
            botsFile.WriteLine("tf_bot_difficulty 3");
            botsFile.WriteLine("tf_bot_quota 8");
            botsFile.WriteLine("tf_bot_quota_mode fill");
            botsFile.WriteLine("tf_bot_join_after_player 1");
            botsFile.Close();
            LogMessage("[Bots] Created missing bots.cfg at %s", botsCfg);
        }
    }

    // nobots.cfg defaults
    if (!FileExists(noBotsCfg))
    {
        File noBotsFile = OpenFile(noBotsCfg, "w");
        if (noBotsFile != null)
        {
            noBotsFile.WriteLine("// Auto-generated nobots.cfg");
            noBotsFile.WriteLine("tf_bot_quota 0");
            noBotsFile.Close();
            LogMessage("[Bots] Created missing nobots.cfg at %s", noBotsCfg);
        }
    }
}
